buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:4.2.0'
        classpath 'com.aries:docker-java-shaded:3.1.0-rc-5'
        classpath 'io.franzbecker:gradle-lombok:1.14'
    }
}

allprojects {
    group = 'by.kachanov'
    version = 1.0
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'jacoco'
    apply plugin: 'com.bmuschko.docker-remote-api'
    apply plugin: 'io.franzbecker.gradle-lombok'

    if (System.getProperty("os.name").toLowerCase(Locale.ENGLISH).contains('windows')
            && Float.valueOf(System.getProperty("os.version")) < 10) {
        docker {
            url = 'tcp://192.168.99.100:2376'
            certPath = file(System.getProperty("user.home") + '\\.docker\\machine\\certs')
        }
    }

    sourceCompatibility = 1.11
    targetCompatibility = 1.11

    repositories {
        mavenCentral()
        jcenter()
    }

    lombok {
        version = '1.18.4'
        sha256 = ''
    }

    test {
        systemProperty 'test.database.port', "${defaultTestDbPort}"
        useJUnitPlatform()
    }
}